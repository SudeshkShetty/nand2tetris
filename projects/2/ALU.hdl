// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16 (a=x, b=false, sel=zx, out=tempzx);
    Not16 (in=tempzx, out=nottempzx);
    Mux16 (a=tempzx, b=nottempzx, sel=nx, out=tempnx);
    Mux16 (a=y, b=false, sel=zy, out=tempzy);
    Not16 (in=tempzy, out=nottempzy);
    Mux16 (a=tempzy, b=nottempzy, sel=ny, out=tempny);
    Add16 (a=tempnx, b=tempny, out=tempnxaddny);
    And16 (a=tempnx, b=tempny, out=tempnxandny);
    Mux16 (a=tempnxandny, b=tempnxaddny, sel=f, out=tempfxy);
    Not16 (in=tempfxy, out=tempnotfxy);
    Mux16 (a=tempfxy, b=tempnotfxy, sel=no, out=out, out[0..7]=tempout1, out[8..15]=tempout2, out[15]=tempmsb);
    Or8Way (in=tempout1, out=tempor8way1);
    Or8Way (in=tempout2, out=tempor8way2);
    Or (a=tempor8way1, b=tempor8way2, out=tempor16way);
    Mux (a=true, b=false, sel=tempor16way, out=zr); 
    Mux (a=false, b=true, sel=tempmsb, out=ng);   
}
